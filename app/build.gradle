plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.0'
    id 'kotlin-kapt'
}

def Properties versionProps = new Properties()

android {
    namespace 'ru.svolf.convertx'
    compileSdkVersion 33
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {

        versionProps.load(new FileInputStream(versionPropsFile))

        def runTasks = gradle.startParameter.taskNames

        def versionMajor = versionProps['VERSION_MAJOR'].toInteger()
        def versionMinor = versionProps['VERSION_MINOR'].toInteger()
        def versionPatch = versionProps['VERSION_PATCH'].toInteger()
        // increment build number on every gradle sync
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        // versionProps['CACHED_VERSION']
        // Используется для поддержки Instant Run в дебаг режиме. Потому что при инкрементальном обновлении постоянно меняется versionCode
        // в AndroidManifest.xml из-за чего применение изменений становится невозможным без полной установки приложения.
        // Этот костыль берет для дебаг версии versionCode с последней релизной сборки и не меняет его на протяжении всего процесса debug сборок,
        // обновляется на новое, в тот момент, когда происходит релизная сборка проекта


        // only in release build
        if (":${project.name}:assembleRelease" in runTasks || 'aR' in runTasks) {
            versionPatch++;
            if (versionPatch >= 9) {
                versionPatch = 0;
                versionMinor++;
            }
            if (versionMinor >= 9) {
                versionMinor = 0;
                versionMajor++;
            }
            // sync if release
            versionProps['CACHED_VERSION'] = versionBuild.toString()
        }

        versionProps['VERSION_MAJOR'] = versionMajor.toString()
        versionProps['VERSION_MINOR'] = versionMinor.toString()
        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "ru.svolf.convertx"
            minSdk 26
            //noinspection ExpiredTargetSdkVersion
            targetSdk 33
            versionCode versionBuild
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            multiDexEnabled true
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            jniDebuggable false
            minifyEnabled false
            zipAlignEnabled true
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        //coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    defaultConfig {
        signingConfig signingConfigs.debug
    }

    buildFeatures {
        viewBinding true
    }
}
def latestFastAdapterRelease = '5.7.0'
def nav_version = '2.5.3'
def room_version = "2.5.0"
def dagger_version = '2.44.2'

dependencies {
    implementation project(path: ':backdrop')
    implementation "androidx.room:room-runtime:$room_version"
    // To use Kotlin annotation processing tool (kapt)
    kapt "androidx.room:room-compiler:$room_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation "com.mikepenz:fastadapter:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-binding:${latestFastAdapterRelease}" // view binding helpers
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'
}
